buildscript {
    ext.kotlin_version = '1.2.21'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.2'

        // Nexus
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.11.0"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4.1'
}

group 'com.marcelkliemannel'
version '1.0.0'

apply plugin: 'kotlin'
apply plugin: 'org.junit.platform.gradle.plugin'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // Required for the google authenticator
    compile 'commons-codec:commons-codec:1.11'

    testCompile 'org.junit.jupiter:junit-jupiter-params:5.0.2'
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.0.2'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.0.2'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}



// Nexus

apply plugin: 'com.bmuschko.nexus'

modifyPom {
    project {
        name "Kotlin one-time password library"
        description 'A Kotlin one-time password library to generate "Google Authenticator", "Time-based One-time Password" (TOTP) and "HMAC-based One-time Password" (HOTP) codes based on RFC 4226 and 6238.'
        url 'https://github.com/marcelkliemannel/kotlin-onetimepassword'
        developers {
            developer {
                id "marcelkliemannel"
                name "Marcel Kliemannel"
                email "dev@marcelkliemannel.com"
            }
        }

        groupId project.group
        artifactId project.name
        version project.version
        packaging 'jar'

        licenses {
            license {
                name 'MIT License'
                url 'https://opensource.org/licenses/MIT'
                distribution 'repo'
            }
        }

        scm {
            connection 'scm:git:git://github.com:marcelkliemannel/kotlin-onetimepassword.git'
            developerConnection 'scm:git:git://github.com:marcelkliemannel/kotlin-onetimepassword.git'
            url 'https://github.com/marcelkliemannel/kotlin-onetimepassword'
        }
    }
}

extraArchive {
    sources = true
    tests = true
    javadoc = true
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
}

apply plugin: 'io.codearte.nexus-staging'