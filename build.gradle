buildscript {
    ext.kotlin_version = '1.2.21'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.2'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4.1'
}

group 'com.marcelkliemannel'
version '1.0.0'

apply plugin: 'kotlin'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'maven'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // Required for the google authenticator
    compile 'commons-codec:commons-codec:1.11'

    testCompile 'org.junit.jupiter:junit-jupiter-params:5.0.2'
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.0.2'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.0.2'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task createPom {
    doLast {
        pom {
            project {
                name "Kotlin one-time password library"
                description 'A Kotlin one-time password library to generate "Time-based One-time Password" (TOTP), "HMAC-based One-time Password" (HOTP) and "Google Authenticator" codes.'
                url 'https://github.com/marcelkliemannel/kotlin-onetimepassword'
                developers {
                    developer {
                        id "marcelkliemannel"
                        name "Marcel Kliemannel"
                        email "dev@marcelkliemannel.com"
                    }
                }

                groupId 'com.marcelkliemannel'
                artifactId 'kotlin-onetimepassword'
                version project.version

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }
            }.writeTo("pom.xml")
        }
    }
}